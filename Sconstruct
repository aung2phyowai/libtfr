
import os

if hasattr(os,'uname'):
    system = os.uname()[0]
else:
    system = 'Windows'

# source files and libraries
src_files = ['mtm.c', 'tfr.c']
hdr_files = ['tfr.h']
libs = ['m']

lib_path = ['/lib','/usr/lib','/usr/lib64','/usr/local/lib','/usr/local/lib64',]

# install location
AddOption('--prefix',
          dest='prefix',
          type='string',
          nargs=1,
          action='store',
          metavar='DIR',
          help='installation prefix')

if not GetOption('prefix')==None:
    install_prefix = GetOption('prefix')
else:
    install_prefix = '/usr/local/'

Help("""
Type: 'scons' to build the library
      'scons install' to install library and headers under %s
      'scons python' to build python extension module
      'scons python-install' to install python extension module
      'scons matlab' to compile mex file
      (use --prefix  to change library installation location)
""" % install_prefix)

# build options
env = Environment(LIBS = ['fftw3'],
                  LIBPATH = lib_path,
                  CCFLAGS = ['-O2', '-std=c99'],
                  tools=['default','mex','pyext'])

# system-specific settings
if system=='Darwin':
    env.Append(LIBS = ['lapack'],
               CPPPATH = ['/opt/local/include'],
               LIBPATH = ['/opt/local/lib'])
elif system=='Linux':
    env.Append(LIBS = ['atlas','cblas','f77blas','lapack'])


sharedobjs = env.SharedObject(src_files)
slib = env.StaticLibrary('libtfr', source = src_files)
dylib = env.SharedLibrary('libtfr', sharedobjs)
test_prog = env.Program('test_tfr', ['test_tfr.c', slib])

env.Alias('lib', [slib, dylib])
env.Alias('install', env.Install(os.path.join(install_prefix, 'include'), hdr_files))
env.Alias('install', env.Install(os.path.join(install_prefix,'lib'), [slib, dylib]))

# Python module
pylib = env.PyExt('_libtfr','libtfr.c',sharedobjs)

env.Alias('python', pylib)
env.Alias('python-install', env.PyInstall([pylib, 'libtfr.py']))

# matlab mex-file
if hasattr(env,'MEX'):
    mex = env.MEX('tfrspec', ['tfrspec.c', sharedobjs])
    env.Alias("mex",mex)
